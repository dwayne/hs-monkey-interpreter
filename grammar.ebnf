/* A context-free grammar for the monkey programming language.
 *
 * Go to https://www.bottlecaps.de/rr/ui to view a syntax diagram of the
 * grammar.
 */

Program ::= Stmt*

Stmt     ::= ( Let | Return | ExprStmt )
Let      ::= 'let' Id '=' Expr ';'
Return   ::= 'return' Expr ';'
ExprStmt ::= Expr ';'?

Expr       ::= Equality
Equality   ::= Comparison ( ( '==' | '!=' ) Comparison )*
Comparison ::= Term ( ( '<' | '>' ) Term )*
Term       ::= Factor ( ( '+' | '-' ) Factor )*
Factor     ::= Unary ( ( '*' | '/' ) Unary )*
Unary      ::= ( '!' | '-' ) Unary | Operator
Operator   ::= Primary ( '(' Args? ')' | '[' Expr ']' )*
Primary    ::= Id | Number | String | Array | Hash | 'true' | 'false'
             | 'if' '(' Expr ')' Block ( 'else' Block )?
             | 'fn' '(' Params? ')' Block
             | '(' Expr ')'
String     ::= '"' [^"]* '"'
Array      ::= '[' ExprList? ']'
Hash       ::= '{' KeyValueList? '}'

Block    ::= '{' Stmt+ '}'
Params   ::= Id ( ',' Id )*
Args     ::= ExprList
ExprList ::= Expr ( ',' Expr )*

KeyValueList ::= KeyValue ( ',' KeyValue )*
KeyValue     ::= Expr ':' Expr

Id     ::= [a-zA-Z_]+
Number ::= [0-9]+
