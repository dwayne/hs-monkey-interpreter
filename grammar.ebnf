Program ::= Stmt*

Stmt     ::= ( Let | Return | ExprStmt )
Let      ::= 'let' Id '=' Expr ';'
Return   ::= 'return' Expr ';'
ExprStmt ::= Expr ';'?

Expr       ::= Equality
Equality   ::= Comparison ( ( '==' | '!=' ) Comparison )*
Comparison ::= Term ( ( '<' | '>' ) Term )*
Term       ::= Factor ( ( '+' | '-' ) Factor )*
Factor     ::= Unary ( ( '*' | '/' ) Unary )*
Unary      ::= ( '!' | '-' ) Unary | Call
Call       ::= Primary ( '(' Args? ')' )*
Primary    ::= Id | Number | String | 'true' | 'false'
             | 'if' '(' Expr ')' Block ( 'else' Block )?
             | 'fn' '(' Params? ')' Block
             | '(' Expr ')'
String     ::= '"' [^"]* '"'

Block  ::= '{' Stmt+ '}'
Params ::= Id ( ',' Id )*
Args   ::= Expr ( ',' Expr )*

Id     ::= [a-zA-Z_]+
Number ::= [0-9]+
